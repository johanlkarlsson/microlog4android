defaultTasks 	/* Core */					'microlog4android-core:clean', 'microlog4android-core:build', 'microlog4android-core:javadoc',
				/* Core tests */			'microlog4android-core-tests:clean', 'microlog4android-core-tests:test',
				/* slf4j impl */			'microlog4android-slf4j-impl:clean', 'microlog4android-slf4j-impl:build', 'microlog4android-slf4j-impl:javadoc',
				/* package modules */		'clean', 'packageDistribution'


allprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'maven'
	
	repositories {
		mavenCentral()
		mavenRepo urls: "file://" + System.getProperty('user.home') + "/.m2/repository/"
	}
	
	sourceCompatibility = source
	version = microlog4androidVersion
}


/*
 * MAIN BUILD 
 */
dependsOn(':microlog4android-slf4j-impl')

dependencies {
	// microlog4android
	compile fileTree(dir: 'microlog4android-core/build/libs', include: 'microlog4android-core*.jar')
	compile fileTree(dir: 'microlog4android-slf4j-impl/build/libs', include: 'microlog4android-slf4j-impl*.jar')
}

jar {
	from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}

tempJarFiles = buildDirName + '/tmp-classes'
tempDistFolder = buildDirName + '/tmp-dist'

task unpackFatJar(dependsOn: jar) << {
	copy {
		jarFile = findMicrologJar()

	    from zipTree(jarFile)
		include '**/*.class'
		into tempJarFiles
	}
}

task deleteUnusedJar(dependsOn: unpackFatJar) << {
	File micrologJar = findMicrologJar()

	if(micrologJar != null) {
		findMicrologJar().delete()
	}
}

/*
 * Returns the microlog4android jar-file.
 */
private File findMicrologJar() {
	File[] distsDirContent = libsDir.listFiles()

	File micrologJar = null
	for(File file : distsDirContent) {
		if(file.getName().contains('microlog4android')) {
			micrologJar = file
		}
	}
	
	return micrologJar
}

task packageJar(dependsOn: deleteUnusedJar, type: Zip) {
	extension = 'jar'
	from tempJarFiles
}

/*
 * Copies the application files to create the dist archive file and to the integration module for the emulator tests
 */
task copyApplicationFiles(dependsOn: packageJar) << {
	// Copies the javadoc from the core module to its own folder
	copy {
		from 'microlog4android-core/build/docs'
		include '**/*'
		into docsDir.getPath() + '/m4a-core'
	}
	
	// Copies the javadoc from the slf4j-impl module to its own folder
	copy {
		from 'microlog4android-slf4j-impl/build/docs'
		include '**/*'
		into docsDir.getPath() + '/m4a-slf4j-impl'
	}
	
	// Creates the temporary dist folder and copies the jar
	new File(tempDistFolder).mkdirs()
	
	copy {
		from distsDir
		include '*.jar'
		into tempDistFolder
	}	
	
	// Copies the packaged jar file into the integration-tests project
	copy {
		from distsDir
		include '*.jar'
		into 'microlog4android-integration-tests/libs'
	}
}

task packageDistribution(dependsOn: copyApplicationFiles, type: Zip) {
	from tempDistFolder
	from docsDir
	baseName = 'microlog4android-dist'
}